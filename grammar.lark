// Lua 5.1 grammar

?start: chunk

?type: PRIMITIVE_TYPE
     | TYPE_NAME
     | array_type
     | dict_type
     | obj_type
     | union_type
     | generic_type
     | meta_type
     | func_type

type_hint: "--" "@type" type

array_type: type "[" "]"
dict_type: "{" "[" type "]" ":" type "}"
obj_type: "{" (obj_type_field ("," obj_type_field)* ","?)? "}"
obj_type_field: NAME ":" type

meta_type: type "@meta" type
func_type: "(" (type ("," type)*)? ")" "->" type

generic_type: NAME "<" type ("," type)* ">"

alias_type: "--" "@alias" NAME generic_param_list "=" type
          | "--[[" "@alias" NAME generic_param_list "=" type "]]"
generic_param_list: "<" (generic_param ("," generic_param)*)? ">" | empty
empty:

record_type: "--" "@record" NAME field_list_type*
field_list_type: "--" "@field" NAME ":" type

generic_type_hint: "--" "@generic" generic_param ("," generic_param)*
generic_param: NAME (":" type)?
return_type_hint: "--" "@return" type
param_types: generic_type_hint? param_type* return_type_hint?
param_type: "--" "@param" NAME ":" type

union_type: type "|" type

chunk: stmt* last_stmt?

?block: chunk

?stmt: block
     | var_decl
     | assign_stmt
     | do_stmt
     | while_stmt
     | repeat_stmt
     | if_stmt
     | range_for_stmt
     | generic_for_stmt
     | func_decl
     | last_stmt
     | break_stmt
     | func_call
     | alias_type
     | record_type

assign_stmt: var "=" expr

do_stmt: "do" block "end"

while_stmt: "while" expr "do" block "end"

repeat_stmt: "repeat" block "until" expr

if_stmt: "if" expr "then" block elseif_branch* else_branch? "end"
elseif_branch: "elseif" expr "then" block
else_branch: "else" block

range_for_stmt: "for" NAME "=" expr "," expr ("," expr)? "do" block "end"
generic_for_stmt: "for" name_list "in" expr_list "do" block "end"

func_decl: "function" func_name func_body

var_decl: type_hint? "local" var "=" expr

?last_stmt: return_stmt
          | break_stmt

break_stmt: "break"

return_stmt: "return" expr?

func_name: NAME ("." NAME)* (":" NAME)?

var_list: var ("," var)*

?var: NAME
    | index_expr
    | prop_expr

?expr: log_expr
     | ELLIPSIS
     | func_expr
     | prefix_expr
     | atom

expr_list: expr ("," expr)*

index_expr: prefix_expr "[" expr "]"
prop_expr: prefix_expr "." NAME

?prefix_expr: var
            | func_call
            | method_call
            | "(" expr ")"

method_call: prefix_expr ":" NAME args
func_call: prefix_expr args

?args: "(" expr_list? ")"
     | table
     | STRING

func_expr: "function" func_body

func_body: "(" param_list ")" param_types block "end"

param_list: name_list ("," ELLIPSIS)?
          | ELLIPSIS
          | empty

name_list: NAME ("," NAME)*

table: "{" field_list "}"

field_list: (field (("," | ";") field)* ("," | ";")?)?

?field: dict_field
      | obj_field
      | expr
dict_field: "[" expr "]" "=" expr
obj_field: IDENTIFIER "=" expr

?log_expr: eq_expr LOG_OP log_expr | eq_expr
?eq_expr: rel_expr EQ_OP eq_expr | rel_expr
?rel_expr: add_expr REL_OP rel_expr | add_expr
?add_expr: mul_expr ADD_OP add_expr | mul_expr
?mul_expr: exp_expr MUL_OP mul_expr | exp_expr
?exp_expr: unary_expr EXP_OP exp_expr | unary_expr
?unary_expr: UNARY_OP unary_expr | atom

?atom: var
     | NIL
     | BOOLEAN
     | STRING
     | NUMBER
     | INTEGER
     | table
     | prefix_expr

TYPE_NAME: /[a-zA-Z_][a-zA-Z0-9_]*/
PRIMITIVE_TYPE: /integer|boolean|number|string|nil|never|unknown/

ELLIPSIS: /\.\.\./

LOG_OP: /and|or/
EQ_OP: /==|~=/
REL_OP: /<=|>=|<|>/
ADD_OP: /\+|-|\.\./
MUL_OP: /\*|\/|%/
EXP_OP: /\^/
UNARY_OP: /-|#|not/

NAME: /[a-zA-Z_][a-zA-Z0-9_]*/
IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_]*/
NIL: /nil/
BOOLEAN: /true|false/
STRING: /"([^"\\]|\\.)*"/
NUMBER: /(\d+)?(\.\d+)/
INTEGER: /\d+/

%ignore /\s+/
%ignore /--(?!\s*(@(param|alias|record|field|type|return))).*/
